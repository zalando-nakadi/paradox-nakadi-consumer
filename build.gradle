import org.gradle.plugins.signing.Sign

buildscript {
    ext {
        springBootVersion = '1.3.5.RELEASE'
        curatorVersion = '2.9.1'
        slf4jVersion = '1.7.21'
        jacksonVersion = '2.6.6'
        httpClientVersion = '4.5.2'
        amazonSQSVersion = '1.11.129'
        gsonVersion = '2.8.0'
        guavaVersion = '19.0'
        commonsLangVersion = '3.4'
        findBugsVersion = '3.0.1'
        junitVersion = '4.12'
        mockitoVersion = '1.10.19'
        assertjVersion = '3.5.1'
        jsonVersion = '20160212'
        rxjavaVersion = '1.1.6'
        javaxRsVersion = '2.0.1'
        okHttpVersion = '3.3.1'
        wireMockVersion = '1.57'
        commonsIOVersion = '2.5'
        mockWebServerVersion = '3.6.0'
        stupsTokensVersion = '0.9.11'
        stupsSpringOauth2ClientVersion = '1.0.9'
        propdepsPluginVersion = '0.0.9.RELEASE'
        jaywayVersion = '2.2.0'
        jacocoVersion = '0.7.9'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:propdeps-plugin:${propdepsPluginVersion}")
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "maven"
    apply plugin: "signing"

    group = "org.zalando.paradox"
    version = rootProject.version

    repositories {
        mavenLocal()
        mavenCentral()
    }

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'

        apply plugin: 'findbugs'

        findbugs {
            reportLevel = "high"
            sourceSets = []
            findbugsMain {
                ignoreFailures = false
                reports {
                    xml.enabled = false
                    html.enabled = !xml.isEnabled()
                }
            }
        }

        dependencies {
            compile("com.google.guava:guava:${guavaVersion}")
            compile("org.apache.commons:commons-lang3:${commonsLangVersion}")
            compile("com.google.code.findbugs:jsr305:${findBugsVersion}")
        }
    }

    compileTestJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'

        dependencies {
            testCompile ("junit:junit:${junitVersion}") {
                exclude group: 'org.hamcrest'
            }
            testCompile("org.mockito:mockito-all:${mockitoVersion}")
            testCompile("org.assertj:assertj-core:${assertjVersion}")
            testCompile("org.slf4j:slf4j-log4j12:${slf4jVersion}")
            testCompile("com.jayway.jsonpath:json-path:${jaywayVersion}")
        }

        test {
            systemProperty("java.awt.headless", "true")
            scanForTestClasses = true
            include(["**/*Test.class", "**/*Tests.class"])
            testLogging {
                // set options for log level LIFECYCLE
                events "passed", "skipped", "failed", "standardOut"
                showExceptions true
                exceptionFormat "short"
                showCauses true
                showStackTraces true
                showStandardStreams false
                // set options for log level DEBUG and INFO
                debug {
                    events "started", "passed", "skipped", "failed", "standardOut", "standardError"
                    exceptionFormat "full"
                }
                info.events = debug.events
                info.exceptionFormat = debug.exceptionFormat

                afterSuite { desc, result ->
                    if (!desc.parent) { // will match the outermost suite
                        def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                        def startItem = '|  ', endItem = '  |'
                        def repeatLength = startItem.length() + output.length() + endItem.length()
                        println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
                    }
                }
            }
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    if (project.hasProperty("sign")) {
        signing {
            sign configurations.archives
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                def ossrhUsername = project.hasProperty('ossrhUsername')?project.ossrhUsername:''
                def ossrhPassword = project.hasProperty('ossrhPassword')?project.ossrhPassword:''

                beforeDeployment {
                    MavenDeployment deployment -> if (project.hasProperty("sign")) { signing.signPom(deployment) }
                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Paradox Nakadi Consumer'
                    packaging 'jar'
                    description 'Paradox high level Nakadi consumer'
                    url 'https://github.com/zalando-incubator/paradox-nakadi-consumer'

                    scm {
                        connection 'https://github.com/zalando-incubator/paradox-nakadi-consumer.git'
                        developerConnection 'https://github.com/zalando-incubator/paradox-nakadi-consumer.git'
                        url 'https://github.com/zalando-incubator/paradox-nakadi-consumer'
                    }

                    licenses {
                        license {
                            name 'The MIT License (MIT)'
                            url 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id 'mbudzyn'
                            name 'Michal Budzyn'
                            email 'michalbudzyn@gmail.com'
                        }
                        developer {
                            id 'gyao'
                            name 'Gary Yao'
                            email 'gary.yao@zalando.de'
                        }
                        developer {
                            id 'balp'
                            name 'Bilal Alp'
                            email 'bilal.alp@zalando.de'
                        }
                    }
                }
            }
        }
    }
}

configure(allprojects) {
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'jacoco'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    jacoco {
        toolVersion = "${jacocoVersion}"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()

        maven { url "http://repo1.maven.org/maven2/" }
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

project(":paradox-nakadi-consumer-core") {
    description = "Paradox Nakadi Consumer Core"

    dependencies {
        compile "org.json:json:${jsonVersion}"
        compile "io.reactivex:rxjava:${rxjavaVersion}"
        compile "javax.ws.rs:javax.ws.rs-api:${javaxRsVersion}"
        compile "com.squareup.okhttp3:okhttp:${okHttpVersion}"
        compile "org.apache.httpcomponents:httpclient:${httpClientVersion}"
        compile "org.slf4j:slf4j-api:${slf4jVersion}"
        compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-joda:${jacksonVersion}"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
        testCompile "com.github.tomakehurst:wiremock:${wireMockVersion}"
        testCompile "commons-io:commons-io:${commonsIOVersion}"
        testCompile "com.squareup.okhttp3:mockwebserver:${mockWebServerVersion}"
    }
}

project(":paradox-nakadi-consumer-partitioned-zk") {
    description = "Paradox Nakadi Consumer Partition Management with Zookeeper"

    dependencies {
        compile(project(":paradox-nakadi-consumer-core"))
        compile "org.apache.curator:curator-framework:${curatorVersion}"
        compile "org.apache.curator:curator-recipes:${curatorVersion}"
        testCompile "org.apache.curator:curator-test:${curatorVersion}"
    }
}

project(":paradox-nakadi-consumer-boot") {
    description = "Paradox Nakadi Consumer Spring Boot"

    dependencies {
        compile(project(":paradox-nakadi-consumer-core"))
        compile(project(":paradox-nakadi-consumer-partitioned-zk"))
        compileOnly(project(":paradox-nakadi-consumer-sqs-error-handler"))
        compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}") {
            exclude module: 'spring-boot-starter-logging'
            exclude module: 'logback-classic'
            exclude module: 'log4j-over-slf4j'
        }
        compile("org.zalando.stups:spring-boot-zalando-stups-tokens:${stupsTokensVersion}")
        compile("org.zalando.stups:stups-spring-oauth2-client:${stupsSpringOauth2ClientVersion}")
        testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")

        runtime("org.apache.httpcomponents:httpclient:${httpClientVersion}")
    }
}

project(":paradox-nakadi-consumer-sqs-error-handler") {
    description = "Paradox Nakadi Consumer SQS Error Handler"

    dependencies {
        compile(project(":paradox-nakadi-consumer-core"))
        compile("com.amazonaws:aws-java-sdk-sqs:${amazonSQSVersion}")
    }
}

project(":paradox-nakadi-consumer-example-boot") {
    description = "Paradox Nakadi Consumer Spring Boot Example "

    dependencies {
        compile(project(":paradox-nakadi-consumer-partitioned-zk"))
        compile(project(":paradox-nakadi-consumer-boot"))
        compile(project(":paradox-nakadi-consumer-sqs-error-handler"))
        compile("com.amazonaws:aws-java-sdk-sqs:${amazonSQSVersion}")

        compile("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        compile("org.springframework.boot:spring-boot-starter-log4j:${springBootVersion}")
        testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    }

    apply plugin: 'spring-boot'

    springBoot.backupSource = false
    bootRepackage {
        withJarTask jar
        mainClass 'de.zalando.paradox.nakadi.consumer.example.boot.Application'
    }
}

subprojects {
    task listAllDependencies(type: DependencyReportTask) {}
}
